plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.8'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
	id 'org.asciidoctor.jvm.convert' version '3.3.2'
	id 'com.google.cloud.tools.jib' version '3.3.1'
}

group = 'spectrum'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	asciidoctorExt
}

repositories {
	mavenCentral()
}

ext {
	set('snippetsDir', file('build/generated-snippets'))
	set('testcontainersVersion', "1.17.6")
}

dependencies {
	// web
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

	// dev
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// db
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'com.mysql:mysql-connector-j'

	// test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.rest-assured:rest-assured' // 통합테스트, rest-assured 사용하기 위해서

	// TestContainers
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:mysql'

	// api docs
	testImplementation 'org.springframework.restdocs:spring-restdocs-restassured'
	asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'

	// jwt
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

	// Web Client
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
}

dependencyManagement {
	imports {
		mavenBom("org.testcontainers:testcontainers-bom:${property("testcontainersVersion")}")
	}
}

//jib 설정
jib {
	from {
		image = "openjdk:11.0.11-jre-slim"
	}
	to {
		image = "meenzino/spectrum"
		tags = ["latest"]
	}
	container {
		jvmFlags = ["-Xms128m", "-Xmx128m"]
	}
}

tasks.named('test') {
	outputs.dir snippetsDir
	useJUnitPlatform()
}

tasks.named('asciidoctor') {
	inputs.dir snippetsDir
	dependsOn test

	// config 추가
	configurations 'asciidoctorExt'

	// 변환할 adoc들을 명시한다.
	sources {
		include '**/index.adoc'
	}

	// include 연산을 사용할 때 base 디렉터리를 지정해줍니다.
	baseDirFollowsSourceFile()
}

asciidoctor.doFirst {
	delete file("${project.rootDir}/src/main/resources/static/docs")
}

tasks.register('copyDocument', Copy) {
	dependsOn asciidoctor

	from "${project.buildDir}/docs/asciidoc"
	into "${project.rootDir}/src/main/resources/static/docs"
}

build {
	dependsOn copyDocument
}
